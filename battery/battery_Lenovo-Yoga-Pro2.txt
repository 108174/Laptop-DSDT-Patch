#Maintained by: RehabMan for: Laptop Patches
#battery_Lenovo-Yoga-Pro2.txt

# Created by ryman95 2015-06-09

# works for:
#  Lenovo Yoga Pro 2

# 16-bit registers
into device label H_EC code_regex BAPV,\s+16 replace_matched begin DC00,8,DC01,8 end;
into device label H_EC code_regex B1CR,\s+16 replace_matched begin DC10,8,DC11,8 end;
into device label H_EC code_regex BARC,\s+16 replace_matched begin FC00,8,FC01,8 end;
into device label H_EC code_regex B1FC,\s+16 replace_matched begin FC10,8,FC11,8 end;
into device label H_EC code_regex BADC,\s+16 replace_matched begin DV00,8,DV01,8 end;
into device label H_EC code_regex BADV,\s+16 replace_matched begin DV10,8,DV11,8 end;
into device label H_EC code_regex B1CY,\s+16 replace_matched begin PR00,8,PR01,8 end;

# install b1b2
into method label B1B2 remove_entry;
into definitionblock code_regex . insert
begin
Method (B1B2, 2, NotSerialized) { Return(Or(Arg0, ShiftLeft(Arg1, 8))) }\n
end;

# fix 16-bit methods
into method label _BST code_regex \(BAPV, replaceall_matched begin (B1B2(DC00,DC01), end;
into method label _BST code_regex \(B1CR, replaceall_matched begin (B1B2(DC10,DC11), end;
into method label _BST code_regex \(BARC, replaceall_matched begin (B1B2(FC00,FC01), end;
into method label _BIF code_regex \(B1FC, replaceall_matched begin (B1B2(FC10,FC11), end;
into method label _BIF code_regex \(BADC, replaceall_matched begin (B1B2(DV00,DV01), end;
into method label _BIF code_regex \(BADV, replaceall_matched begin (B1B2(DV10,DV11), end;
into method label GBID code_regex \(B1CY, replaceall_matched begin (B1B2(PR00,PR01), end;

# rename 256-bit
into device label H_EC code_regex (SMD0,)\s+(56) replace_matched begin SMDX,%2,//%1%2 end;

# utility methods to read/write buffers from/to EC
into method label RE1B parent_label H_EC remove_entry;
into method label RECB parent_label H_EC remove_entry;
into device label H_EC insert
begin
Method (RE1B, 1, NotSerialized)\n
{\n
    OperationRegion(ERAM, EmbeddedControl, Arg0, 1)\n
    Field(ERAM, ByteAcc, NoLock, Preserve) { BYTE, 8 }\n
    Return(BYTE)\n
}\n
Method (RECB, 2, Serialized)\n
// Arg0 - offset in bytes from zero-based EC\n
// Arg1 - size of buffer in bits\n
{\n
    ShiftRight(Arg1, 3, Arg1)\n
    Name(TEMP, Buffer(Arg1) { })\n
    Add(Arg0, Arg1, Arg1)\n
    Store(0, Local0)\n
    While (LLess(Arg0, Arg1))\n
    {\n
        Store(RE1B(Arg0), Index(TEMP, Local0))\n
        Increment(Arg0)\n
        Increment(Local0)\n
    }\n
    Return(TEMP)\n
}\n
end;

# set offset
into method label MHPF code_regex \(SMD0, replaceall_matched begin (RECB(0x1c,256), end;
into method label CFUN code_regex \(SMD0, replaceall_matched begin (RECB(0x1c,256), end;
